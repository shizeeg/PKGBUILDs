# Maintainer: <n17ikh@gmail.com>
# Contributor: <igor.mogielnicki@gmail.com>
# Contributor: Shizeeg Unadequatov <shizeeque@gmail.com>

pkgname=mhddfs-git
pkgver=r10.33a72f3
pkgrel=1
pkgdesc="fuse filesystem that combines several mount points into a single one."
arch=('i686' 'x86_64')
url="http://mhddfs.uvw.ru/"
license=('GPL3')
depends=('fuse' 'libcap')
makedepends=('uthash')

source=("$pkgname::git+https://github.com/trapexit/mhddfs.git")
sha256sums=('SKIP')

pkgver() {
  cd "${srcdir}/${pkgname}"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${pkgname}"
  make
}

package() {
  cd "${srcdir}/${pkgname}"
  install -D -m0755 mhddfs	"${pkgdir}/usr/bin/mhddfs"
#  install -D -m0644 LICENSE	"${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -D -m0644 COPYING	"${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
  install -D -m0644 ChangeLog	"${pkgdir}/usr/share/doc/${pkgname}/ChangeLog"
  install -D -m0644 README	"${pkgdir}/usr/share/doc/${pkgname}/README"
  install -D -m0644 README.ru.UTF-8 "${pkgdir}/usr/share/doc/${pkgname}/README.ru.UTF-8"
  install -D -m0444 mhddfs.1 "${pkgdir}/usr/share/man/man1/mhddfs.1"
}
